(in-package :tagit)

(defroute (:get "/api/projects/users/([0-9a-f-]+)") (req res args)
  (catch-errors (res)
    (let ((user-id (car args))
          (get-notes (ignore-errors (< 0 (parse-integer (cl-ppcre:regex-replace-all "[^0-9]" (get-var req "get_notes") ""))))))
      (unless (string= (user-id req) user-id)
        (error 'insufficient-privileges :msg "You are trying to access another user's projects. For shame."))
      (alet* ((projects (get-user-projects user-id get-notes)))
        (send-json res projects)))))

(defroute (:post "/api/projects/users/([0-9a-f-]+)") (req res args)
  (catch-errors (res)
    (alet* ((user-id (car args))
            (project-data (post-var req "data")))
      (unless (string= (user-id req) user-id)
        (error 'insufficient-privileges :msg "You are trying to access another user's projects. For shame."))
      (alet ((project (add-project user-id project-data)))
        (send-json res project)))))

(defroute (:put "/api/projects/([0-9a-f-]+)") (req res args)
  (catch-errors (res)
    (alet* ((user-id (user-id req))
            (project-id (car args))
            (project-data (post-var req "data")))
      (alet ((project (edit-project user-id project-id project-data)))
        (send-json res project)))))

(defroute (:delete "/api/projects/([0-9a-f-]+)") (req res args)
  (catch-errors (res)
    (alet* ((project-id (car args))
            (user-id (user-id req))
            (nil (delete-project user-id project-id)))
      (send-json res t))))

(defroute (:put "/api/projects/([0-9a-f-]+)/permissions/persona/([0-9a-f-]+)") (req res args)
  (catch-errors (res)
    (alet* ((user-id (user-id req))
            (project-id (car args))
            (persona-id (cadr args))
            (permissions (post-var req "permissions"))
            (perms (set-project-persona-permissions user-id project-id persona-id permissions)))
      (send-json res perms))))

(defroute (:put "/api/projects/([0-9a-f-]+)/keys/persona/([0-9a-f-]+)") (req res args)
  (catch-errors (res)
    (alet* ((project-id (car args))
            (persona-id (cadr args))
            (challenge (post-var req "challenge"))
            (keydata (post-var req "keydata"))
            (success (project-add-persona-key project-id persona-id challenge keydata)))
      (send-json res success))))

